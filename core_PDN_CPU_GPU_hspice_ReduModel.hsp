* Top level spice file 

.option post=1
*.chkanode type=gate dv=0.5*pvdd report=active
.option parhier = local 
.option measform = 1
.option lis_new
.option post probe

*************************************** User defined functions ****************
.param V_meas(node, ref) = 'v(node) - v(ref)'


*************************************** Input Models**************************
	***** define load current source model used in die model *****
.inc ./hspice_inc/out_curr_profile_CPU_55ns_17A_2000ns_cnst_1000ns_12_17_rand_55ns_rampDown_noClk.txt

	*.inc ./hspice_inc/out_curr_profile_CPU_55ns_13A_1000ns_cnst_500ns_9_13_rand_55ns_rampDown_noClk.txt

	*.inc ./hspice_inc/out_curr_profile_CPU_72ns_13A_1000ns_cnst_500ns_9_13_rand_72ns_rampDown_noClk.txt
	*.inc ./hspice_inc/out_curr_profile_CPU_144ns_13A_1000ns_cnst_500ns_9_13_rand_144ns_rampDown_noClk.txt

	*.inc ./hspice_inc/out_curr_profile_CPU_55ns_11A_1000ns_cnst_500ns_9_13_rand_55ns_rampDown_noClk.txt
	*.inc ./hspice_inc/out_curr_profile_CPU_55ns_9A_1000ns_cnst_500ns_6_9_rand_55ns_rampDown_noClk.txt

	*** GPU profile
.inc ./hspice_inc/out_curr_profile_GPU_20ns_2p5A_5000ns_cnst_noClk.txt

	***** define die model *****
.inc ./hspice_inc/model_die_CPU.inc 
.inc ./hspice_inc/model_die_GPU.inc 

	***** define pkg model *****
	*.inc ./hspice_inc/C_v1_forCPU_wBF_wDSU_wGPU_RmLF_1e4Hz_IdEM.cir
	*.param pkg_model = str('C_v1_forCPU_wBF_wDSU_wGPU_RmLF_1e4Hz_IdEM')

.inc ./hspice_inc/Mam_MCM_Andes_0724_cpugpu_cut3_run2_IdEM.cir
.param pkg_model = str('Mam_MCM_Andes_0724_cpugpu_cut3_run2_IdEM')

	***** define PCB model *****
	*.inc ./hspice_inc/ACM2_PT00547399_A_01_cut_CPU_PSI_RmLF_manip_IdEM0p5.cir 
	*.param PCB_model = str('ACM2_PT00547399_A_01_cut_CPU_PSI_RmLF_manip_IdEM0p5')

	*.inc ./hspice_inc/ACM2_PT00547399_A_01_Layout_Study_20230626_cut_CPU_RmDC_mani_IdEM_0p5.cir		*** 0626 Cadera improvement
	*.param PCB_model = str('ACM2_PT00547399_A_01_Layout_Study_20230626_cut_CPU_RmDC_mani_IdEM_0p5')

	*.inc ./hspice_inc/ACM2_PT00547399_A_01_Layout_Study_20230626_cut_CPU_RmL5L10_RmDC_mani_IdEM0p5.cir 	*** 0626, but remove L5, L10 pwr shape
	*.param PCB_model = str('ACM2_PT00547399_A_01_Layout_Study_20230626_cut_CPU_RmL5L10_RmDC_mani_IdEM0p5')

	*.inc ./hspice_inc/MAMMOTH_PCB_230721_CPU_GPU_merge_discrete_IdEM_0p5.cir
	*.param PCB_model = str('MAMMOTH_PCB_230721_CPU_GPU_merge_discrete_IdEM_0p5')

	*.inc ./hspice_inc/MAMMOTH_PCB_230721_CPU_GPU_merge_discrete_rmDC_manip_IdEM_0p5.cir
	*.param PCB_model = str('MAMMOTH_PCB_230721_CPU_GPU_merge_discrete_rmDC_manip_IdEM_0p5')

.inc ./hspice_inc/MAMMOTH_PCB_230803_CPU_GPU_merge_discrt_DCrm_manip_IdEM.cir
.param PCB_model = str('MAMMOTH_PCB_230803_CPU_GPU_merge_discrt_DCrm_manip_IdEM')

	***** define reduced pkg&PCB model *****	
.inc ./hspice_inc/Z_plot_GPU_CPU_reduModel_rmLF_manip_IdEM0p5.cir
.param Redu_model = str('Z_plot_GPU_CPU_reduModel_rmLF_manip_IdEM0p5')


*************************************** Input params **************************
.param Vdd 	= 0.75

.param CPU_die = str('model_die_lumped_cpu')
.param GPU_die = str('model_die_lumped_gpu')

.param Cdie_cpu	 = '226.n'
.param Rdie_cpu	 = '0.664m'
.param Rleak_cpu = 0.232

.param Cdie_gpu  = '187.n'
.param Rdie_gpu  = '0.81m'
.param Rleak_gpu = 1.27

	*** exp. to turn off GPU die model
	*.param Cdie_gpu	= '0.n'
	*.param Rdie_gpu	= '1.e9m'
	*.param Rleak_gpu= 1.e9

.param tStep	= 10.p
.param tStop	= 10.u

.param Lcomp_val= 10.p		*** temporarily mimic Andes pkg design rule, use 0 if pkg model is appropriate

*************************************** cap model *****************************
.inc ../models_cap/GCM155D70E106ME36_DC0V_125degC_0402_10uF.mod
.inc ../models_cap/GCM155D70G475ME36_DC0V_125degC_0402_4p7uF.mod
.inc ../models_cap/GCM155C71A105KE38_DC0V_125degC_0402_1uF.mod
.inc ../models_cap/GCM31CD70G476ME02_DC0V_125degC_1206_47uF.mod
.inc ../models_cap/GCM32ED70E107ME36_DC0V_125degC_1210_100uF.mod
.inc ../models_cap/GCM033D70E105ME36_DC0V_125degC_0201_1uF.mod 
.inc ../models_cap/NFM15HC105D0G3_feedthrough_DC0V_85degC_3T_0402_1uF.mod
.inc ../models_cap/LLC152D70G105ME01_DC0V_125degC_0204_1uF.mod
.inc ../models_cap/LLL153C70G104ME01_DC0V_125degC_0204_0.1uF.mod
.inc ../models_cap/GRT033R70J103KE01_DC0V_125degC_0201_0p01uF.mod
.inc ../models_cap/GCM155R71C104KA55_DC0V_125degC_0402_0.1uF.mod
.inc ../models_cap/GCM21BD70G226ME36_DC0V_125degC_0805_22uF.mod
.inc ../models_cap/GCM32EM8EA227ME08_DC0V_125degC_1210_220uF.mod
.inc ../models_cap/GCM155R71E153KA55_DC0V_125degC_0402_0.015uF.mod
.inc ../models_cap/GRT033C70J104KE01_DC0V_125degC_0201_0p1uF.mod
.inc ../models_cap/mlcc_470uF_mockup_T598D477M2R5ATE009.mod
.inc ../models_cap/open_circuit.mod

.inc ../models_cap/EC1001.mod
.inc ../models_cap/EC1002.mod
.inc ../models_cap/EC1100_200nF.mod

	***** define cap model strings for better ref ****
.param mlcc_1uF_0402 		= str('GCM155C71A105KE38_DC0V_125degC_0402_1uF')
.param mlcc_1uF_0204 		= str('LLC152D70G105ME01_DC0V_125degC_0204_1uF')
.param mlcc_1uF_0201 		= str('GCM033D70E105ME36_DC0V_125degC_0201_1uF')
.param mlcc_1uF_0402_3T		= str('NFM15HC105D0G3_feedthrough_DC0V_85degC_3T_0402_1uF')
.param mlcc_4p7uF_0402 		= str('GCM155D70G475ME36_DC0V_125degC_0402_4p7uF')
.param mlcc_10uF_0402 		= str('GCM155D70E106ME36_DC0V_125degC_0402_10uF')
.param mlcc_47uF_1206		= str('GCM31CD70G476ME02_DC0V_125degC_1206_47uF')
.param mlcc_100uF_1210		= str('GCM32ED70E107ME36_DC0V_125degC_1210_100uF')
.param mlcc_0p1uF_0402		= str('GCM155R71C104KA55_DC0V_125degC')
.param mlcc_0p1uF_0201		= str('GRT033C70J104KE01_DC0V_125degC')
.param mlcc_0p1uF_0204 		= str('LLL153C70G104ME01_DC0V_125degC')
.param mlcc_0p015uF_0402	= str('GCM155R71E153KA55_DC0V_125degC')
.param mlcc_0p01uF_0201		= str('GRT033R70J103KE01_DC0V_125degC')
.param mlcc_22uF_0805		= str('GCM21BD70G226ME36_DC0V_125degC_0805_22uF')
.param mlcc_470uF_mockup	= str('mlcc_470uF_mockup_T598D477M2R5ATE009') 
.param mlcc_220uF_1210		= str('GCM32EM8EA227ME08_DC0V_125degC')

.param siCap_empwr_EC1001 = str('EC1001')
.param siCap_empwr_EC1002 = str('EC1002')
.param siCap_empwr_EC1100_200nF = str('EC1100_200nF')

*************************************** PMIC **********************************
Vref_gnd 	ref_gnd		0	0.				*** opt 1/2 ideal PMIC w/o remote sense feedback
	*E_fdbk	ref_gnd	0	VCVS DELAY pmic_pwr pkg_bump_1_0  td=50.n	*** opt 2/2 VCVS to model feedback

VshortPMIC	pmic_pwr	ref_gnd	Vdd		*** can be switched to PMIC model 

*************************************** Socket ********************************
Rskt	bga_pcb		bga_pkg		0.				*** can add socket model 

*************************************** PCB & pkg reduced model ***********************************
XPCB_PKG_ReduModel
 + pkg_bump 
 + pkg_bump_gpu 
 + pmic_pwr
 + ref_gnd
 + str(Redu_model)

*************************************** Die ***********************************
Xblk_die 	pkg_bump	ref_gnd str(CPU_die) C_die= 'Cdie_cpu'	R_die_1= 'Rdie_cpu' R_die_2= 1.e-6	R_leak= 'Rleak_cpu'
Xblk_die_gpu 	pkg_bump_gpu	ref_gnd str(GPU_die) C_die= 'Cdie_gpu'	R_die_1= 'Rdie_gpu' R_die_2= 1.e-6	R_leak= 'Rleak_gpu'

*************************************** Analysis I: AC simulation *************
.if ( 0 )
	.if ( 1 )
	*** NOTE: DO not include port def if tran analysis later
	P1 pkg_bump	ref_gnd	port=1	z0 = 0.1
	P2 pkg_bump_gpu	ref_gnd	port=2	z0 = 0.1

	
	.lin 	sparcalc=1 	filename=impedance_plot	noisecalc=0 	gdcalc=0	format=touchstone 	dataformat=MA 
	*.probe  ac s11(db) s11(p) s21(db) s21(p) 
	.probe ac zin(1)(m) zin(1)(p) zin(2)(m) zin(2)(p)
	.endif 

	.ac 	dec 50 1. 1.G
	*I_pkg_bump_1_0 		pkg_bump_1_0	ref_gnd		AC=1
	*.probe AC v(pkg_bump_1_0)

.endif

*************************************** Analysis II: Transient simulation *****
.if ( 1 )
	.tran tStep tSTOP 

	.probe tran v(pkg_bump) 	v(pkg_bump_gpu) 	v(bga_pkg)
	.probe x(Xblk_die.pin_bump)						
	.probe x(Xblk_PCB.a_3)  
	.probe x(xblk_PCB.a_1)  	x(xblk_PCB.a_2) 


	.param vdd_meas_start = 35.ns
	.param vdd_meas_end   = 150.ns
	.meas tran pkg_bump_cpu_p2p 	PP	V(pkg_bump)	from='vdd_meas_start' to='vdd_meas_end'
	.meas tran pkg_bump_cpu_vmax	MAX	V(pkg_bump)	from='vdd_meas_start' to='vdd_meas_end'
	.meas tran pkg_bump_cpu_vmin	MIN	V(pkg_bump)	from='vdd_meas_start' to='vdd_meas_end'
	.meas tran pkg_bump_cpu_vmax_t	WHEN	V(pkg_bump) = 'pkg_bump_cpu_vmax' 	from='vdd_meas_start' to='vdd_meas_end'
	.meas tran pkg_bump_cpu_vmin_t	WHEN	V(pkg_bump) = 'pkg_bump_cpu_vmin' 	from='vdd_meas_start' to='vdd_meas_end'

	*.probe i(L_comp)	i(L_comp_gpu)
	*.probe i(*)
	*.probe x(*)

.endif 