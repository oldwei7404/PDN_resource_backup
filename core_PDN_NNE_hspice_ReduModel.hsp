* Top level spice file 

.option post=1
*.chkanode type=gate dv=0.5*pvdd report=active
.option parhier = local 
.option measform = 1
.option lis_new
.option post probe

*************************************** User defined functions ****************
.param V_meas(node, ref) = 'v(node) - v(ref)'


*************************************** Input Models**************************
	***** define load current source model used in die model *****
* .param pwl_file_in_PA = str('./hspice_inc/out_curr_profile_NNE_4x2grp_PA_test4_act_0p3_0p2_0518_200clk_0scale_continue_idle_clk_51703_0925.inc.csv')
* .param pwl_file_in_MA = str('./hspice_inc/out_curr_profile_NNE_4x2grp_MA_test4_act_0p3_0p2_0518_200clk_0scale_continue_idle_clk_51703_0925.inc.csv')

		* .param pwl_file_in_PA = str('./hspice_inc/curr_profile_NNE_4x2_PA_shortened.txt.csv')		*** PMIC vendor test, 11us
		* .param pwl_file_in_MA = str('./hspice_inc/curr_profile_NNE_4x2_MA_shortened.txt.csv')

		.param pwl_file_in_PA = str('./hspice_inc/curr_profile_NNE_4x2_PA_1031_500MHz.txt.csv')		*** PMIC vendor test,  11us
		.param pwl_file_in_MA = str('./hspice_inc/curr_profile_NNE_4x2_MA_1031_500MHz.txt.csv')

		* .param pwl_file_in_PA = str('./hspice_inc/curr_profile_NNE_4x2_PA_noRampFr0515_1116.csv')		*** PMIC vendor test, no ramp ,  11us
		* .param pwl_file_in_MA = str('./hspice_inc/curr_profile_NNE_4x2_MA_noRampFr0515_1116.csv')
		
		*** idle current
	*.inc ./hspice_inc/out_curr_profile_idle_20us_0A_noClk.txt

		***  step up current (only one def allowed)
	*.inc ./hspice_inc/out_curr_profile_NNE_100ns_12A_10000ns_cnst_100ns_rampDown_noClk.txt
	*.inc ./hspice_inc/out_curr_profile_NNE_300nsRamp_100ns_12A_10000ns_cnst_100ns_rampDown_noClk.txt
	*.inc ./hspice_inc/out_curr_profile_NNE_300nsRamp_100ns_12A_10000ns_cnst_100ns_rampDown_Clk0p7floor.txt

	***** define die model *****
.subckt model_die_lumped_nne_tile
+ pin_bump ref_gnd C_die=100n R_die_1=1.25m R_die_2=RESMIN R_leak=0.84	pwl_file_in = str('./currSrc.csv')	

R_die_1_ 	pin_bump	2	R_die_1
R_die_2_	2		3	R_die_2
C_die_		3		ref_gnd	C_die 
R_leak_		pin_bump	ref_gnd	R_leak	
IcurrSrc	pin_bump	ref_gnd	PWL pwlfile = str(pwl_file_in) 
.ends

	***** define reduced pkg&PCB model *****	
	* .inc ./hspice_inc/Z_NNE_4x2_reducedModel_1029_IdEM0p5.cir
	* .param Redu_model = str('Z_NNE_4x2_reducedModel_1029_IdEM0p5')

	* .inc ./hspice_inc/Z_NNE_4x2_reducedModel_0912_jgwei_manip_IdEM.cir
	* .param Redu_model = str('Z_NNE_4x2_reducedModel_0912_jgwei_manip_IdEM')

.inc ./hspice_inc/Z_NNE_4x2_reducedModel_240102_manip_IdEM0p5_wDC_s16p.cir

*************************************** Input params **************************
.param Vdd 	= 0.775

* .para Cdie_coeff = 3.7	
* .para Rdie_coeff = 0.27	
.para Cdie_coeff = 1.
.para Rdie_coeff = '1./Cdie_coeff'	

.param Cdie_PA 	= '123.n * Cdie_coeff'
.param Cdie_MA 	= '96.n * Cdie_coeff'
.param Rdie_PA 	= '1.02m * Rdie_coeff'
.param Rdie_MA 	= '1.3m * Rdie_coeff'
.param Rleak 	= 0.84

.param tStep	= 10.p
.param tStop	= 50.0u

*************************************** cap model *****************************
.inc ../models_cap/GCM155D70E106ME36_DC0V_125degC_0402_10uF.mod
.inc ../models_cap/GCM155D70G475ME36_DC0V_125degC_0402_4p7uF.mod
.inc ../models_cap/GCM155C71A105KE38_DC0V_125degC_0402_1uF.mod
.inc ../models_cap/GCM31CD70G476ME02_DC0V_125degC_1206_47uF.mod
.inc ../models_cap/GCM32ED70E107ME36_DC0V_125degC_1210_100uF.mod
.inc ../models_cap/GCM033D70E105ME36_DC0V_125degC_0201_1uF.mod 
.inc ../models_cap/NFM15HC105D0G3_feedthrough_DC0V_85degC_3T_0402_1uF.mod
.inc ../models_cap/LLC152D70G105ME01_DC0V_125degC_0204_1uF.mod
.inc ../models_cap/LLL153C70G104ME01_DC0V_125degC_0204_0.1uF.mod
.inc ../models_cap/GRT033R70J103KE01_DC0V_125degC_0201_0p01uF.mod
.inc ../models_cap/GCM155R71C104KA55_DC0V_125degC_0402_0.1uF.mod
.inc ../models_cap/GCM21BD70G226ME36_DC0V_125degC_0805_22uF.mod
.inc ../models_cap/GCM32EM8EA227ME08_DC0V_125degC_1210_220uF.mod
.inc ../models_cap/GCM155R71E153KA55_DC0V_125degC_0402_0.015uF.mod
.inc ../models_cap/GRT033C70J104KE01_DC0V_125degC_0201_0p1uF.mod
.inc ../models_cap/GCM033C70J104KE02_DC0V_125degC_0p1uF_0201.mod
.inc ../models_cap/mlcc_470uF_mockup_T598D477M2R5ATE009.mod
.inc ../models_cap/open_circuit.mod

.inc ../models_cap/EC1001.mod
.inc ../models_cap/EC1002.mod
.inc ../models_cap/EC1100_200nF.mod

	***** define cap model strings for better ref ****
.param mlcc_1uF_0402 		= str('GCM155C71A105KE38_DC0V_125degC_0402_1uF')
.param mlcc_1uF_0204 		= str('LLC152D70G105ME01_DC0V_125degC_0204_1uF')
.param mlcc_1uF_0201 		= str('GCM033D70E105ME36_DC0V_125degC_0201_1uF')
.param mlcc_1uF_0402_3T		= str('NFM15HC105D0G3_feedthrough_DC0V_85degC_3T_0402_1uF')
.param mlcc_4p7uF_0402 		= str('GCM155D70G475ME36_DC0V_125degC_0402_4p7uF')
.param mlcc_10uF_0402 		= str('GCM155D70E106ME36_DC0V_125degC_0402_10uF')
.param mlcc_47uF_1206		= str('GCM31CD70G476ME02_DC0V_125degC_1206_47uF')
.param mlcc_100uF_1210		= str('GCM32ED70E107ME36_DC0V_125degC_1210_100uF')
.param mlcc_0p1uF_0402		= str('GCM155R71C104KA55_DC0V_125degC')
.param mlcc_0p1uF_0201		= str('GRT033C70J104KE01_DC0V_125degC')
.param mlcc_0p1uF_0204 		= str('LLL153C70G104ME01_DC0V_125degC')
.param mlcc_0p015uF_0402	= str('GCM155R71E153KA55_DC0V_125degC')
.param mlcc_0p01uF_0201		= str('GRT033R70J103KE01_DC0V_125degC')
.param mlcc_22uF_0805		= str('GCM21BD70G226ME36_DC0V_125degC_0805_22uF')
.param mlcc_220uF_1210		= str('GCM32EM8EA227ME08_DC0V_125degC')

.param mlcc_470uF_mockup	= str('mlcc_470uF_mockup_T598D477M2R5ATE009') 

.param siCap_empwr_EC1001 = str('EC1001')
.param siCap_empwr_EC1002 = str('EC1002')
.param siCap_empwr_EC1100_200nF = str('EC1100_200nF')

*************************************** PMIC **********************************

Vref_gnd 	ref_gnd		0	0.				
	*E_fdbk	ref_gnd	0	VCVS DELAY pmic_pwr pkg_bump_1_0  td=50.n	*** opt 2/2 VCVS to model feedback

.subckt PMIC_model
+ pin_out ref_gnd vdd_pmic = 0.75
	VshortPMIC	pin_out	ref_gnd	vdd_pmic
.ends 

*** opt 1 ideal PMIC w/o remote sense feedback
.if ( 1 )
	*VshortPMIC	pmic_pwr	ref_gnd	Vdd		*** can be switched to PMIC model 
	xblk_PMIC pmic_pwr ref_gnd PMIC_model  vdd_pmic = 'Vdd' 
.endif 
*** opt 2 EMpower simplified IVR equivalent model 	(remember to disable PMIC bulk caps on PCB)
.if ( 0 )
	VempowerVS	pmicEMpwrNode1	ref_gnd	Vdd		*** can be switched to PMIC model 
	LempowerVS1  pmicEMpwrNode1  pmicEMpwrNode2  	'0.027778n * 0.9091'  
	RempowerVS1  pmicEMpwrNode2  pmic_pwr			50.u 
.endif 
*************************************** Socket ********************************
Rskt	bga_pcb		bga_pkg		0.				*** can add socket model 

*************************************** Reduced Model *************************
XreduModel 
 + pkg_bump_0_0
 + pkg_bump_0_1
 + pkg_bump_0_2
 + pkg_bump_0_3 
 + pkg_bump_1_0
 + pkg_bump_1_1
 + pkg_bump_1_2
 + pkg_bump_1_3 
 + pmic_pwr
 + pmic_pwr
 + pmic_pwr
 + pmic_pwr
 + pcbCap_C86 
 + pcbCap_C1294
 + pcbCap_C93
 + pcbCap_C97 
 + ref_gnd
 + Z_NNE_4x2_reducedModel_240102_manip_IdEM0p5_wDC_s16p

	* XpcbCap_C86  	pcbCap_C86      ref_gnd 	str(mlcc_100uF_1210)
	* XpcbCap_C1294  	pcbCap_C1294   	ref_gnd 	str(mlcc_100uF_1210)
	* XpcbCap_C93  	pcbCap_C93      ref_gnd 	str(mlcc_100uF_1210)
	* XpcbCap_C97  	pcbCap_C97      ref_gnd 	str(mlcc_100uF_1210)
 
*************************************** Die ***********************************
Xblk_die_0_0	pkg_bump_0_0	ref_gnd	model_die_lumped_nne_tile	C_die= 'Cdie_MA'	R_die_1= 'Rdie_MA' 	R_die_2= 1.e-5	R_leak= 'Rleak'		pwl_file_in = str(pwl_file_in_MA)
Xblk_die_0_1	pkg_bump_0_1	ref_gnd	model_die_lumped_nne_tile	C_die= 'Cdie_MA'	R_die_1= 'Rdie_MA' 	R_die_2= 1.e-5	R_leak= 'Rleak'		pwl_file_in = str(pwl_file_in_MA)
Xblk_die_0_2	pkg_bump_0_2	ref_gnd	model_die_lumped_nne_tile	C_die= 'Cdie_MA'	R_die_1= 'Rdie_MA' 	R_die_2= 1.e-5	R_leak= 'Rleak'		pwl_file_in = str(pwl_file_in_MA)
Xblk_die_0_3	pkg_bump_0_3	ref_gnd	model_die_lumped_nne_tile	C_die= 'Cdie_MA'	R_die_1= 'Rdie_MA' 	R_die_2= 1.e-5	R_leak= 'Rleak'		pwl_file_in = str(pwl_file_in_MA)
Xblk_die_1_0	pkg_bump_1_0	ref_gnd	model_die_lumped_nne_tile	C_die= 'Cdie_PA'	R_die_1= 'Rdie_PA' 	R_die_2= 1.e-5	R_leak= 'Rleak'		pwl_file_in = str(pwl_file_in_PA)
Xblk_die_1_1	pkg_bump_1_1	ref_gnd	model_die_lumped_nne_tile	C_die= 'Cdie_PA'	R_die_1= 'Rdie_PA' 	R_die_2= 1.e-5	R_leak= 'Rleak'		pwl_file_in = str(pwl_file_in_PA)
Xblk_die_1_2	pkg_bump_1_2	ref_gnd	model_die_lumped_nne_tile	C_die= 'Cdie_PA'	R_die_1= 'Rdie_PA' 	R_die_2= 1.e-5	R_leak= 'Rleak'		pwl_file_in = str(pwl_file_in_PA)
Xblk_die_1_3	pkg_bump_1_3	ref_gnd	model_die_lumped_nne_tile	C_die= 'Cdie_PA'	R_die_1= 'Rdie_PA' 	R_die_2= 1.e-5	R_leak= 'Rleak'		pwl_file_in = str(pwl_file_in_PA)


.param is_ac_run = 0
*************************************** Analysis I: AC simulation *************
.if ( is_ac_run == 1 )
	.if ( 1 )
		*** NOTE: DO not include port def if tran analysis later
		P1 pkg_bump_0_0	ref_gnd	port=1	z0 = 0.1
		P2 pkg_bump_0_1	ref_gnd	port=2	z0 = 0.1
		P3 pkg_bump_0_2	ref_gnd	port=3	z0 = 0.1
		P4 pkg_bump_0_3	ref_gnd	port=4	z0 = 0.1
		P5 pkg_bump_1_0	ref_gnd	port=5	z0 = 0.1
		P6 pkg_bump_1_1	ref_gnd	port=6	z0 = 0.1
		P7 pkg_bump_1_2	ref_gnd	port=7	z0 = 0.1
		P8 pkg_bump_1_3	ref_gnd	port=8	z0 = 0.1

		
		.lin 	sparcalc=1 	filename=impedance_plot	noisecalc=0 	gdcalc=0	format=touchstone 	dataformat=MA 
		*.probe  ac s11(db) s11(p) s21(db) s21(p) 
		.probe ac zin(1)(m) zin(1)(p) zin(5)(m) zin(5)(p) 
	.endif 

	.if ( 0 )
		*i_ac_pkg_bump_0_0 pkg_bump_0_0 ref_gnd dc=0 ac=1
		i_ac_pkg_bump_1_0 pkg_bump_1_0 ref_gnd dc=0 ac=1
		.probe ac v(pkg_bump_0_0)	v(pkg_bump_1_0)
	.endif 

	.ac 	dec 50 1. 1.G
		
	
	*I_pkg_bump_1_0 		pkg_bump_1_0	ref_gnd		AC=1
	*.probe AC v(pkg_bump_1_0)

.endif 

*************************************** Analysis II: Transient simulation *****
.if ( is_ac_run != 1 )
	.tran tStep tSTOP 


	.probe tran v(pkg_bump_1_0) v(pkg_bump_1_3) v(pkg_bump_0_0) v(pkg_bump_0_3)  v(bga_pkg)
	.probe x(Xblk_die_1_0.pin_bump)	x(Xblk_PCB.a_2)	x(Xblk_pkg.a_1)
	.probe x(xblk_PMIC.pin_out)


	*.probe i(*)
	*.probe x(*)

.endif 